import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

def baseDir = "$buildDir/stage/sdkman-$release"

task prepareBin(type: Copy) {
	from('src/main/bash')
	into("$baseDir/bin")
	include('**/sdkman-init.sh')
}

task prepareScripts(type: Copy) {
	from('src/main/bash')
	into("$baseDir/src")
	include('**/*')
	exclude('**/sdkman-init.sh')
	filter(
			ReplaceTokens,
			tokens: [
					SDKMAN_VERSION       : sdkmanVersion,
					SDKMAN_CANDIDATES_API: candidatesApi
			]
	)
}

task prepareContrib(type: Copy) {
	from('contrib')
	into("$baseDir/contrib")
}

tasks.test.configure {
	dependsOn(prepareScripts)
	testLogging.exceptionFormat = TestExceptionFormat.FULL
}

task assembleArchive(type: Zip, dependsOn: [prepareBin, prepareScripts, prepareContrib]) {
	archiveVersion = sdkmanVersion
	from('build/stage') {
		include('**/*')
	}
}

tasks.assemble.configure {
	dependsOn(assembleArchive)
}
